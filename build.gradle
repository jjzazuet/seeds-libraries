subprojects {

	apply plugin: 'java'
	apply plugin: 'osgi'
	apply plugin: 'maven'
	apply plugin: 'signing'
	apply plugin: 'eclipse'

	group = 'net.tribe7.seeds'
	version = '15.0'
	sourceCompatibility = 1.6

	def releaseBuild = true

	repositories { mavenCentral() }
	configurations { provided }

	sourceSets {
		main.compileClasspath += configurations.provided
		test.compileClasspath += configurations.provided
		test.runtimeClasspath += configurations.provided
	}

	dependencies { provided 'com.google.code.findbugs:jsr305:2.0.1' }
	compileJava.options.encoding = 'UTF-8'	
	javadoc.options.encoding = 'UTF-8'	

	eclipse {
		classpath {
			plusConfigurations += configurations.provided
			noExportConfigurations += configurations.provided
		}
	}

	jar { manifest.attributes provider: 'tribe7.net' }

	javadoc { classpath += configurations.provided }

	task sourcesJar(type: Jar, dependsOn:classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn:javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	artifacts {
		archives jar
		archives sourcesJar
		archives javadocJar
	}

	signing { sign configurations.archives }

	tasks.withType(Zip) { 
		task -> task.doLast {
			ant.checksum file: it.archivePath
		}
	}

	uploadArchives {
		repositories {
			if (!releaseBuild) { mavenLocal() }
			else {
				mavenDeployer {
					beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
					repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
					  authentication(userName: sonatypeUsername, password: sonatypePassword)
					}
					pom.project {
						name project.name
						packaging 'jar'
						description 'Atomic jars for Google Guava libraries'
						url 'http://seeds.tribe7.net/'
						scm {
							url 'scm:git@github.com:jjzazuet/seeds-libraries.git'
							connection 'scm:git@github.com:jjzazuet/seeds-libraries.git'
							developerConnection 'scm:git@github.com:jjzazuet/seeds-libraries.git'
						}
						licenses {
							license {
								name 'The Apache Software License, Version 2.0'
								url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
								distribution 'repo'
							}
						}
						developers {
							developer {
								id 'jjzazuet'
								name 'Jesus Zazueta'
							}
						}
					}
				}
			}
		}
	}
}